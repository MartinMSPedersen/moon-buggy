dnl Process this file with autoconf to produce a configure script.
AC_REVISION($Revision: 1.34 $)
AC_INIT(moon-buggy.h)
AM_INIT_AUTOMAKE(moon-buggy, 0.4.52)
AM_CONFIG_HEADER(config.h)

dnl Some systems want this as we use curses.
AC_DEFINE(_XOPEN_SOURCE, 1)

AC_ARG_WITH(setgid,
[  --with-setgid=NAME      setgid usage: install moon-buggy as group NAME],
[case "${withval}" in
  no)  GAMEGROUP="" ;;
  yes)  AC_MSG_ERROR(missing argument for --with-setgid) ;;
  *)   GAMEGROUP="$withval" ;;
esac])
AC_SUBST(GAMEGROUP)

dnl Checks for programs.
AC_PROG_CC

dnl Checks for libraries.
AC_CHECK_LIB(m, exp)
AC_CHECK_LIB(curses,initscr,CURSLIB=-lcurses)
if test "x$CURSLIB" = x; then
unset ac_cv_lib_curses_initscr
AC_CHECK_LIB(curses,initscr,CURSLIB="-lcurses -ltermcap",,-ltermcap)
fi
if test "x$CURSLIB" = x; then
AC_CHECK_LIB(ncurses,initscr,CURSLIB=-lncurses)
fi
if test "x$CURSLIB" != x; then
LIBS="$CURSLIB $LIBS"
fi

dnl Checks for header files.
AC_HEADER_STDC
AC_CHECK_HEADERS(getopt.h errno.h)

# Check if <sys/select.h> needs to be included for fd_set
AC_MSG_CHECKING([for fd_set])
AC_TRY_COMPILE([#include <sys/types.h>],
        [fd_set readMask, writeMask;], mb_ok=yes, mb_ok=no)
if test $mb_ok = yes; then
    AC_MSG_RESULT([yes, found in sys/types.h])
else
    AC_EGREP_HEADER(fd_mask, sys/select.h, mb_ok=yes)
    if test $mb_ok = yes; then
        AC_DEFINE(HAVE_SYS_SELECT_H)
        AC_MSG_RESULT([yes, found in sys/select.h])
    else
        AC_DEFINE(NO_FD_SET)
        AC_MSG_RESULT(no)
    fi
fi

dnl Checks for typedefs, structures, and compiler characteristics.
AC_C_CONST
AC_TYPE_UID_T
AC_TYPE_SIZE_T

dnl Checks for library functions.
AC_TYPE_SIGNAL
AC_REPLACE_FUNCS(mvwaddnstr wgetnstr)
AC_CHECK_FUNCS(fclean ftruncate getopt_long setreuid)

AM_CONDITIONAL(short_getopt, test "x$ac_cv_func_getopt_long" != xyes)

AC_OUTPUT(Makefile maint.in uninst.pre)
